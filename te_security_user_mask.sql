USE [DEMO]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE OR ALTER PROCEDURE [dbo].[te_security_user_mask]
/*
Build   By     WO#    Notes
------ ----- -------  ----------------------------------------------------------------------------------------
  001   JLR   11008   Completed based on code extracted from te_sec_mask. Added support for new field
                      security_mask.active_flag.

This sproc provides basic IO support for table security_user_mask.

Action Codes
------------
ALL -- get ALL assignments
ASG -- ASsiGn
RMV -- ReMoVe assignment(s)
UPD -- UPDate
                                        |A|A|R|U|
                              Param key |L|S|M|P|
                                        |L|G|V|D|*/
  (                                     ---------
    @action           dtTASK  = null, --|R|R|R|R|
    @user_id          dtID    = null, --|1|R|1|R|
    @security_mask_id dtID    = null, --|1|R|1|R|
    @access_code      char(1) = null  --| |R| |R|
  )

/* Parameter Notes
R = Required
O = Optional
1 = Optional, but one parameter MUST be sent.
*/

AS

DECLARE
  @CchvCrLf       VARCHAR(2), -- const: carriage-return line-feed
  @CdtmBOT        DATETIME  , -- const: beginning of time (1900-01-01)
  @CdtmEOT        DATETIME  , -- const: end of time (2079-06-06)
  @intError       INT       , -- error value generated by SQL
  @intFetchStatus INT       , -- cursor fetch status
  @intNewId       INT       , -- ID of newly-inserted records
  @intNumRec      INT       , -- number of records operated upon
  @intRetVal      INT       , -- sproc return value
  @intStatus      INT       , -- return value from called processes
  @intUserId      dtID      , -- current user's ID; used for auditing
  @intXActLevel   INT       ; -- tracks x-act level; controls explicit transactions when nesting

begin
  SET NOCOUNT ON;

  -- Prep Constants
  SELECT
      @CchvCrLf = CHAR(13) + CHAR(10) ,
      @CdtmBOT  = '1900-01-01'        ,
      @CdtmEOT  = '2079-06-06'        ;

  -- See if there's a parent transaction running
  SET @intXActLevel = @@trancount;

  if @action = 'ALL' -- Get all assignments
    BEGIN
      -- Parse params
      IF @security_mask_id IS NULL AND @user_id IS NULL
        RETURN -7414; -- Either ID or mask must be specified.
      IF @security_mask_id IS NOT NULL AND @user_id IS NOT NULL
        RETURN -7415; -- Either ID or mask must not be specified.

      -- If mask sent, get all users assigned to mask
      IF @security_mask_id IS NOT NULL
        BEGIN
          SELECT
              SecUM.user_id         ,
              SecUM.security_mask_id,
              SecUM.access_code     ,
              SU.user_name
            FROM    dbo.security_user_mask  AS SecUM
              JOIN  dbo.security_user       AS SU     ON SecUM.user_id = SU.user_id
            WHERE SecUM.security_mask_id = @security_mask_id
            ORDER BY SU.user_name;
        END; -- IF @security_mask_id IS NOT NULL

      -- If user sent, get all masks assigned to user
      else if @user_id is not null
        begin
          select
              SecUM.user_id               as user_id                ,
              SecUM.security_mask_id      as security_mask_id       ,
              case SM.active_flag
                when 0 then 'X'
                else SecUM.access_code
              end                         as access_code            ,
              SM.security_mask_id         as security_mask_id       ,
              SM.description              as description            ,
              SM.name                     as name                   ,
              SM.security_mask_role       as security_mask_role     ,
              SM.security_mask_group      as security_mask_group    ,
              SM.security_mask_structure  as security_mask_structure,
              SM.security_mask_item       as security_mask_item
            from    dbo.security_user_mask  as SecUM
              join  dbo.security_mask       as SM     on   SecUM.security_mask_id = SM.security_mask_id
            where SecUM.user_id = @user_id
            order by SM.name;

        end; -- IF @user_id IS NOT NULL

      -- Capture & process the results of the SELECT
      SELECT  @intNumRec  = @@rowcount,
              @intError   = @@error   ;
      IF @intError <> 0
        RETURN @intError - 100000; -- unknown error
      IF @intNumRec = 0
        BEGIN
          IF @security_mask_id IS NOT NULL
            IF NOT EXISTS (SELECT 0 FROM dbo.security_mask WHERE security_mask_id = @security_mask_id)
              RETURN -7111; -- Security mask does not exist.
          if @user_id is not null
            if not exists (select 0 from dbo.security_user where user_id = @user_id)
              return -7211; -- User does not exist.
        end; -- IF @intNumRec = 0

      -- Generate the return value
      set @intRetVal = @intNumRec;

    end; -- @action = 'ALL'

  else if @action = 'ASG' -- Assign mask
    BEGIN

      -- Attempt to insert the record
      INSERT
        INTO dbo.security_user_mask
          (user_id, security_mask_id, access_code)
        VALUES
          (@user_id, @security_mask_id, @access_code);

      -- Capture & process the results of the INSERT
      SELECT  @intNumRec  = @@rowcount,
              @intError   = @@error   ;
      IF @intError = 547 -- Referential integrity violation
        BEGIN
          IF NOT EXISTS (SELECT 0 FROM dbo.security_user WHERE user_id = @user_id)
            RETURN -7211; -- User does not exist.
          IF NOT EXISTS (SELECT 0 FROM dbo.security_mask WHERE security_mask_id = @security_mask_id)
            RETURN -7111; -- Security mask does not exist.
          IF NOT EXISTS (SELECT 0 FROM dbo.ref_access_code WHERE access_code = @access_code)
            RETURN -7121; -- Invalid security mask access code.
          return -119; -- Referential integrity violation.
        end; -- IF @intError = 547
      else if @intError = 515 -- Missing value
        BEGIN
          IF @user_id IS NULL
            RETURN -7213; -- User must be specified.
          IF @security_mask_id IS NULL
            RETURN -7113; -- Security mask must be specified.
          IF @access_code IS NULL
            RETURN -7123; -- Security mask access code must be specified.
          return -120; -- Missing value.
        end; -- ELSE IF @intError = 515
      else if @intError = 2627 -- SQL Duplicate Key
        RETURN -124; -- Already assigned.
      else if @intError = 2601 -- SQL Duplicate Key in Unique Index
        RETURN -126; -- Value already exists.
      else if @intError <> 0
        return @intError - 100000; -- unknown error

      -- Generate the return value
      set @intRetVal = @intNumRec;

    end; -- @action = 'ASG'

  else if @action = 'RMV' -- Remove assignment
    BEGIN
      -- Parse params
      IF @security_mask_id IS NULL AND @user_id IS NULL
        RETURN -7414; -- Either ID or mask must be specified.

      -- Attempt the DELETE
      DELETE
        FROM dbo.security_user_mask
        WHERE user_id = ISNULL(@user_id, user_id)
          AND security_mask_id = ISNULL(@security_mask_id, security_mask_id);

      -- Capture & process the results of the DELETE
      SELECT  @intNumRec  = @@rowcount,
              @intError   = @@error   ;
      IF @intError <> 0
        RETURN @intError - 100000; -- unknown error
      IF @intNumRec = 0
        BEGIN
          IF @security_mask_id IS NOT NULL
            IF NOT EXISTS (SELECT 0 FROM dbo.security_mask WHERE security_mask_id = @security_mask_id)
              RETURN -7111; -- Security mask does not exist.
          IF @user_id IS NOT NULL
            IF NOT EXISTS (SELECT 0 FROM dbo.security_user WHERE user_id = @user_id)
              RETURN -7211; -- User does not exist.
          if @security_mask_id is not null and @user_id is not null
            return -123; -- Not currently assigned.
        end;

      -- Generate the return value
      set @intRetVal = @intNumRec;

    end; -- @action = 'RMV'

  else if @action = 'UPD' -- Assign mask
    begin
      -- Attempt to update the record
      update dbo.security_user_mask
        set access_code = @access_code
        where user_id = @user_id
          and security_mask_id = @security_mask_id;

      -- Capture & process the results of the UPDATE
      select  @intNumRec  = @@rowcount,
              @intError   = @@error   ;
      if @intError = 547 -- Referential integrity violation
        begin
          if not exists (select 0 from dbo.security_user where user_id = @user_id)
            return -7211; -- User does not exist.
          if not exists (select 0 from dbo.security_mask where security_mask_id = @security_mask_id)
            return -7111; -- Security mask does not exist.
          if not exists (select 0 from dbo.ref_access_code where access_code = @access_code)
            return -7121; -- Invalid security mask access code.
          return -119; -- Referential integrity violation.
        end; -- IF @intError = 547
      else if @intError = 515 -- Missing value
        begin
          if @user_id is null
            return -7213; -- User must be specified.
          if @security_mask_id is null
            return -7113; -- Security mask must be specified.
          if @access_code is null
            return -7123; -- Security mask access code must be specified.
          return -120; -- Missing value.
        end; -- ELSE IF @intError = 515
      else if @intError = 2627 -- SQL Duplicate Key
        return -124; -- Already assigned.
      else if @intError = 2601 -- SQL Duplicate Key in Unique Index
        return -126; -- Value already exists.
      else if @intError <> 0
        return @intError - 100000; -- unknown error
      else if @intNumRec = 0
        return -123; -- Not currently assigned.

      -- Generate the return value
      set @intRetVal = @intNumRec;

    end; -- @action = 'UPD'

  else
    return -117; -- Invalid action

  return @intRetVal; -- Row count

end; -- te_security_user_mask
go
